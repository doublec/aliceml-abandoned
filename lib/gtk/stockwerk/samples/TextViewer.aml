(*
 * Authors:
 *   Thorsten Brunklaus <brunklaus@ps.uni-sb.de>
 *
 * Copyright:
 *   Thorsten Brunklaus, 2000
 *
 * Last Change:
 *   $Date$ by $Author$
 *   $Revision$
 *
 *)

import structure Gtk        from "../Gtk"
import structure Gdk        from "../Gdk"
import structure GtkBuilder from "GtkBuilder"

local
    open GtkBuilder

    fun newServer () =
	let
	    val tail   = Promise.promise ()
	    val stream = Ref.ref tail

	    fun process s = (case s of
				 nil   => ()
			       | p::pr => (p (); process pr))
	    fun send p =
		let
		    val newtail = Promise.promise ()
		    val tail    = Ref.exchange(stream, newtail)
		in
		    Promise.fulfill(tail, p::(Promise.future newtail))
		end
	in
	    (spawn (process(Promise.future tail));
	     send)
	end

    val serve = newServer ()

    fun fileToString (file, lines) =
	(case TextIO.inputLine file of
	     NONE      => (TextIO.closeIn file; rev lines)
	   | SOME line => fileToString(file, line::lines))

    val keys = ["abstype", "and", "andalso", "as", "case", "datatype",
		"do", "else", "end", "exception", "fn", "fun", "handle",
		"if", "in", "infix", "infixr", "let", "local", "nonfix",
		"of", "op", "open", "orelse", "raise", "rec", "then",
		"type", "val", "with", "withtype", "while",
		"eqtype", "functor", "include", "sharing",
		"sig", "signature", "structure", "struct", "where",
                "any", "constructor", "fct", "from", "import", "non",
                "pack", "unpack", "when", "withfun", "withval"
	       ]

    fun searchKey(key, nil)   = false
      | searchKey(key, k::kr) = (key = k) orelse searchKey(key, kr)

    fun isKeyWord key = searchKey(key, keys) 

    val isSep = not o Char.isAlphaNum

    fun isToken c = not (isSep c)

    fun myInsertWithTags (buffer, iter, token, taglist) =
    let
	val startOffset = Gtk.textIterGetOffset iter
	val start = Gtk.textIterNew()
    in
	Gtk.textBufferInsert (buffer, iter, token, String.size token) ;
	Gtk.textBufferGetIterAtOffset (buffer, start, startOffset) ;
        List.app (fn t => Gtk.textBufferApplyTag(buffer,t,start,iter)) taglist
    end

    fun insertTokens(pars as (text, iter, tc, nc, bg, ft), nil)       = ()
      | insertTokens(pars as (text, iter, tc, nc, bg, ft), token::tr) =
	let
	    val fg = if isKeyWord token then tc else nc
	in
	    (Gtk.textBufferGetEndIter(text, iter);
	     myInsertWithTags(text, iter, token, [ft, fg, bg]);
	     insertTokens(pars, tr))
	end

    fun collectSeps(ts, nil)         = (String.implode (rev ts), nil)
      | collectSeps(ts, cs as c::cr) =
	if isSep c then collectSeps(c::ts, cr) else (String.implode (rev ts), cs)

    fun collectTokens(ts, nil)         = (String.implode (rev ts), nil)
      | collectTokens(ts, cs as c::cr) =
	if isToken c then collectTokens(c::ts, cr) else (String.implode (rev ts), cs)

    fun lineToTokens(nil)         = nil
      | lineToTokens(cs as c::cr) =
	(case isSep c of
	     true  =>
		 let
		     val (str, cr) = collectSeps(nil, cs)
		 in
		     [str] @ lineToTokens(cr)
		 end
	   | false =>
		 let
		     val (str, cr) = collectTokens(nil, cs)
		 in
		     [str] @ lineToTokens(cr)
		 end)

    fun insertLines(pars, nil)      = ()
      | insertLines(pars, line::lr) =
	(insertTokens(pars, lineToTokens (String.explode line)); insertLines(pars, lr))

    val textcolor = ref (Gdk.colorNew(0xf0f0, 0x2020, 0xa0a0))
    val curwin    = ref NONE : Gtk.object option ref
    val curfile   = ref ""

    fun fileToWindow (file) =
	let
	    val xdim    = 800.0
	    val ydim    = 600.0
	    val lines   = fileToString(TextIO.openIn file, nil)
	    val textv   = MakeWidget (Text {xdim=xdim, ydim=ydim, text=nil})
            val text    = Gtk.textViewGetBuffer textv
	    val xa      = Gtk.adjustmentNew(0.0, 0.0, xdim, 1.0, xdim, xdim)
	    val ya      = Gtk.adjustmentNew(0.0, 0.0, ydim, 1.0, ydim, ydim)
	    val vs      = Gtk.vscrollbarNew(ya)
	    val entries = [TableSimple {x=0, y=0, item=Window textv},
			   TableSimple {x=1, y=0, item=Window vs}]

	    val map   = Gdk.colormapGetSystem ()
	    val black = Gdk.colorNew(0, 0, 0)
	    val white = Gdk.colorNew(65535, 65535, 65535)
	    val kcol  = !textcolor

	    fun createTag(name, prop, value) =
		Gtk.textBufferCreateTag(text, name, prop, value)
	in
	    (Gtk.widgetSetSizeRequest(textv, Real.round xdim, Real.round ydim);
	     Gtk.widgetSetScrollAdjustments(textv, xa, ya);
	     Gdk.colormapAllocColor(map, black, false, true);
	     Gdk.colormapAllocColor(map, white, false, true);
	     Gdk.colormapAllocColor(map, kcol, false, true);
	     insertLines((text, Gtk.textIterNew(),
			  createTag("kc", "foreground-gdk", Gtk.OBJECT kcol),
			  createTag("bl", "foreground-gdk", Gtk.OBJECT black),
			  createTag("wh", "background-gdk", Gtk.OBJECT white),
			  createTag("fn", "font", Gtk.STRING "Courier 14")), 
			 lines);
	     Table {rows=1, columns=2, homogeneous=false, entries=entries})
	end

    fun displayFile () =
	let
	    val note   = valOf(!curwin)
	    val window = fileToWindow(!curfile)
	    val align  = MakeWidget(Alignment {xalign=0.0,
					       yalign=0.0, xscale=1.0,
					       yscale=1.0, inner=window})
	    val label  = MakeWidget(Label "")
	in
	    (Gtk.notebookRemovePage(note, 0);
	     Gtk.widgetShowAll(align);
	     Gtk.notebookAppendPage(note, align, label))
	end

    local
	val enter        = ref true
	val fileSel      = Gtk.fileSelectionNew "Select File to view"
	val okButton     = Gtk.fileSelectionGetFieldOkButton fileSel
	val cancelButton = Gtk.fileSelectionGetFieldCancelButton fileSel

	fun okHandler _ = serve
	    (fn () =>
	     let
		 val name = Gtk.fileSelectionGetFilename fileSel
	     in
		 (curfile := name;
		  Gtk.widgetUnmap fileSel;
		  displayFile ();
		  enter := true)
	     end)
	    
	fun cancelHandler _ =
	    serve (fn () => (Gtk.widgetUnmap fileSel;
			     enter := true))

	val _  = Gtk.signalConnect(okButton, "clicked", okHandler)
	val _  = Gtk.signalConnect(cancelButton, "clicked", cancelHandler)

    in
	fun readFileName () =
	    (case !enter of
		 false => ()
	       | true  =>
		     (enter := false;
		      Gtk.widgetShowAll fileSel;
		      Gtk.widgetMap fileSel;
		      ()))
    end

    local
	datatype entercase = FRESH | TRUE | FALSE

	val enter = ref FRESH

	val colorSel = Gtk.colorSelectionNew ()
	val dialog   = Hole.hole ()
	    
	exception Error

	fun okHandler _ = serve
	    (fn () =>
	     let
		 val color = Gdk.colorNew(0,0,0)
	     in
		 (Gtk.colorSelectionGetCurrentColor(colorSel,color);
		  textcolor := color;
		  displayFile ();
		  enter := TRUE;
		  Gtk.widgetUnmap dialog)
	     end)
	    
	fun cancelHandler _ = serve
	    (fn () => (enter := TRUE;
		       Gtk.widgetUnmap dialog))
	    
	val okButton     = Button("Ok", [Handler("clicked", okHandler)])
	val cancelButton = Button("Cancel",
				  [Handler("clicked", cancelHandler)])
	val bBox         = HButtonBox(EndStyle, [okButton, cancelButton])
	val vBox         =
	    VBox {homogeneous=false, spacing=4,
		  entries=[BoxSimple (Window colorSel), BoxSimple bBox]}
	val toplevel     =
	    Toplevel {title="Select TextColor",
		      border=4, handler=nil, inner=vBox}
    in
	fun getTextColor () =
	    (case !enter of
		 FRESH => (Hole.fill(dialog, MakeWidget toplevel);
			   enter := TRUE;
			   getTextColor ())
	       | FALSE => ()
	       | TRUE  =>
		     (enter := FALSE;
		      Gtk.widgetMap dialog))
    end

    val viewer = Promise.promise ()

    fun openHandler _  = readFileName ()
    fun colorHandler _ = getTextColor ()
    fun closeHandler _ = OS.Process.exit OS.Process.success

    val submenu = [MenuItem("Open...", [Handler("activate", openHandler)]),
		   MenuItem("Text color...",
			    [Handler("activate", colorHandler)]),
		   MenuItem("Exit", [Handler ("activate", closeHandler)])]
    val menu    = MenuBar([{label="File", submenu=submenu}])
    val malign  =
	Alignment {xalign=0.0, yalign=0.0, xscale=1.0, yscale=0.0, inner=menu}

    val _        = curfile := "TextViewer.aml"
    val twin     = fileToWindow "TextViewer.aml"
    val twalign  =
	Alignment {xalign=0.0, yalign=0.0, xscale=1.0, yscale=1.0, inner=twin}
    val notebook = MakeWidget (Notebook(false, [{label="", note=twalign}]))

    val entries  = [BoxSimple malign, BoxSimple (Window notebook)]
    val topbox   = VBox {homogeneous=false, spacing=0,  entries=entries}
    val dHandler = Handler("delete-event", closeHandler)
    val toplevel =
	Toplevel {title="TextViewer", border=2, handler=[dHandler],
		  inner=topbox}
in
    val _ = curwin := SOME notebook
    val _ = Promise.fulfill(viewer, MakeWidget toplevel)
end
