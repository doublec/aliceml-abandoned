(*
 * Author:
 *   Guido Tack <tack@ps.uni-sb.de>
 *
 * Copyright:
 *   Guido Tack, 2003
 *
 *  See the file "LICENSE" for information on usage and
 *  redistribution of this file, and for a
 *     DISCLAIMER OF ALL WARRANTIES.
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

import signature SEARCH       from "SEARCH-sig"
import structure Space        from "Space"

structure Search :> SEARCH =
struct

    fun searchAll p =
	let
	    val s = Space.new()
	    val root = p s
	    fun dfe s =
		case Space.status s of
		    Space.FAILED => (Space.discard s; nil)
		  | Space.SOLVED => [s]
		  | Space.BRANCH (alt, descr) =>
		    let
			val c = Space.clone s
		    in
			Space.commit(s, 0, descr);
			Space.commit(c, 1, descr);
			(dfe s)@(dfe c)
		    end
	in
	    (dfe (Space.clone s), root)
	end

    fun searchOne p =
	let
	    val s = Space.new()
	    val root = p s
	    exception Found of Space.space
	    fun dfe depth s =
		case Space.status s of
		    Space.FAILED => Space.discard s
		  | Space.SOLVED => raise (Found s)
		  | Space.BRANCH(alt,descr) =>
		    let
			val c = Space.clone s
		    in
			Space.commit(s, 0, descr);
			Space.commit(c, 1, descr);
			dfe (depth+1) s;
			dfe (depth+1) c
		    end
	in
	    (dfe 0 (Space.clone s); NONE) handle (Found s) => SOME (s, root)
	end

    fun searchBest p =
	let
	    val s = Space.new()
	    val (root, constrain) = p s
	    fun babe fs bs best =
		case fs of
		    nil =>
		    (case bs of nil => best
			      | b::br => (constrain(b, valOf best);
					  babe [b] br best))
		  | f::fr =>
		    case Space.status f of
			Space.FAILED => (Space.discard f;
					 babe fr bs best)
		      | Space.SOLVED => babe nil (fr@bs) (SOME f)
		      | Space.BRANCH(alt,descr) =>
			let
			    val c = Space.clone f
			in
			    Space.commit(f, 0, descr);
			    Space.commit(c, 1, descr);
			    babe (f::c::fr) bs best
			end
	in
	    case babe [Space.clone s] nil NONE of
		NONE   => NONE
	      | SOME s => SOME (s, root)
	end

    structure Recompute = struct

      fun recompute s nil = Space.clone s
        | recompute s ((i,id)::ir) =
          let
              val c = recompute s ir
          in
              Space.commit(c,i,id);
              c
          end
              
      fun searchOne (p, m) =
          let
	      val s = Space.new()
	      val root = p s
              exception Found of Space.space
              fun dfre s r is d =
                  case Space.status s of
                      Space.SOLVED => (raise Found s)
                    | Space.FAILED => Space.discard s
                    | Space.BRANCH(alt,desc) =>
                      if d>=m then
                          let
                              val c = Space.clone s
                              val intdesc = (0, desc)
                          in
                              Space.commit(s, 0, desc);
                              dfre s c [intdesc] 1;
                              Space.commit(c, 1, desc);
                              dfre c c nil m
                          end
                      else let val intdesc = (0, desc) in                
                               Space.commit(s, 0, desc);
                               dfre s r (intdesc::is) (d+1);
                               let
                                   val c = recompute r is
                                   val intdesc = (1, desc)
                               in
                                   Space.commit(c, 1, desc);
                                   dfre c r (intdesc::is) (d+1)
                               end
                           end
              val s' = Space.clone s
          in
              (dfre s' s' nil m; NONE)
              handle Found s => SOME (s, root)
          end
              
      fun searchAll (p, m) =
          let
	      val s = Space.new()
	      val root = p s
              fun dfre s r is d =
                  case Space.status s of
                      Space.SOLVED => [s]
                    | Space.FAILED => (Space.discard s; nil)
                    | Space.BRANCH(alt,desc) =>
                      if d>=m then
                          let
                              val c = Space.clone s
                              val intdesc = (0, desc)
                          in
                              (Space.commit(s, 0, desc);
                               dfre s c [intdesc] 1)@
                              (Space.commit(c, 1, desc);
                               dfre c c nil m)
                          end
                      else let val intdesc = (0, desc) in                
                               (Space.commit(s, 0, desc);
                                dfre s r (intdesc::is) (d+1))@
                               let
                                   val c = recompute r is
                                   val intdesc = (1, desc)
                               in
                                   Space.commit(c, 1, desc);
                                   dfre c r (intdesc::is) (d+1)
                               end
                           end
              val s' = Space.clone s
          in
              (dfre s' s' nil m, root)
          end

    end

end
