import structure Debug       from "Debug"
import signature MKSPACENODE from "MKSPACENODE-sig"
import signature POLICY      from "MKSPACENODE-sig"
import structure Space       from "x-alice:/lib/gecode/Space"
import structure Path        from "Path"

structure MkSpaceNode :> MKSPACENODE = fct (Policy:POLICY) =>
struct

  open Debug

  exception Child

  type path = int list
  datatype space_node = Node of node_rep
  withtype node_rep =
    {mom    : space_node ,  (* Ancestor *)
     altNum : int ,         (* Which alternative, 0 means root node *)
     space  : Space.space option ref , (* Working space *)
     copy   : Space.space option ref ,
     status : Space.status ,
     bestId : int ,  (* ID number of the best solution known at this node. *)
     best   : Policy.sol option , (* Solution applied to this node. *)
     depth  : int }

  type t = space_node

  fun dbPrint (mask, msg) = Debug.dbPrint (mask, Policy.mask, msg)

  val undefined = lazy assert false

  val best = ref undefined
  val bestId = ref 0  (* ID Number of the current best solution *)

  val root =
    let 
      val status = Space.status Policy.root
      val spaceref = ref (SOME (Space.clone Policy.root))
      val copyref = ref (SOME Policy.root)
    in
    Node {mom    = undefined ,
          altNum = 0 ,
          space  = spaceref ,
          copy   = copyref ,
          status = status ,
          bestId = !bestId ,
          best   = NONE ,
          depth  = 0 }
    end

  fun status    (Node node) = #status node
  fun depth     (Node node) = #depth  node
  fun getBestId (Node node) = #bestId node

  (* Branch and Bound: are we in B&B (bab); bound is the bounding function *)
  val (bab, bound) =
    case Policy.bound of
      NONE => (false, undefined)
    | SOME b => (true, b)
        
  fun setBest sol =
    (best := sol ;
     bestId := !bestId + 1 ;
     dbPrint (dbBaB, "Updated best solution.\n"))
    
  (* Tail-recursive getPath *)
  fun getTailPath (Node node, tail) =
    let
      val altNum = #altNum node
      val newtail = case #best node of   (* May trigger computation of lazy *)
                      NONE   => Path.Branch altNum :: tail
                    | SOME b => Path.Branch altNum :: Path.Better b :: tail
    in
      if altNum = 0 then tail
      else getTailPath (#mom node, newtail)
    end

  fun getPath node = getTailPath (node, [])
   
  (* Recomputes the space of this node. *)
  fun recompute node =
    let
      val mom = #mom node
      val space = getWorkingSpace mom
    in
      dbPrint(dbRecomp,
              "Recomputing node at PATH " ^ 
              (Path.print (getPath (Node node))) ^ "\n");
      case status mom of
        Space.BRANCH descr =>
          (Space.commit (space, #altNum node, descr) ;
           
           (* Branch and Bound. *)
           (case #best node of
              NONE     => space
            | SOME sol => (bound (space, sol) ; space)))

      | _ => assert false
    end

  and getWorkingSpace (Node node) =
    let
      val rspace = #space node
      val rcopy  = #copy node
    in
      (*    wspace & copy *)
      case (!rspace, !rcopy) of 
        (NONE  , SOME s) => Space.clone s  (* clone copy   *)
      | (NONE  , NONE  ) => recompute node (* recomputation *)
      | (SOME s, acopy ) =>                (* donate space *)
          (rspace := NONE ;
           if acopy = NONE andalso Policy.copyq (#depth node)
             then (dbPrint (dbClone, "CLONE at path " ^ 
                            (Path.print (getPath (Node node))) ^ "\n") ;
                   rcopy := SOME (Space.clone s))
             else () ;
           s)
          
    end

  fun updateRef (r,v) = (assert (!r = NONE) ; r:=SOME v ; v)

  fun getSpace (Node node) = 
    let
      val _ = #status node (* Avoid recomputation if this node is new *)
      val rcopy  = #copy node
      val rspace = #space node
      val readOnlySpace =
        (*     copy & wspace *)
        case (!rcopy, !rspace) of
          (SOME s, _     ) => s
        | (NONE  , SOME s) => updateRef (rcopy, Space.clone s)
        | (NONE  , NONE  ) => updateRef (rcopy, recompute node)
    in
      Space.clone readOnlySpace
    end

  (* Return a pair (bestId, bestSol) that
   * is computed lazily when bestSol is requested,
   * so that it uses the up-to-date best solution.  *)
  fun getIdSol momNode =
    let
      val bestIdp = Promise.promise ()
      val momBestId = getBestId momNode
      val bestSol = lazy
        let
          (* Is there a best solution to apply? *)
          val currentBestId = !bestId
          val currentBest = !best (* Harmless race condition *)        
          val _ = assert currentBestId >= momBestId
        in
          if currentBestId > momBestId
            then
              (Promise.fulfill (bestIdp, currentBestId) ;
               SOME currentBest)
            else
              (Promise.fulfill (bestIdp, momBestId) ;
               NONE)
        end
    in
      (Promise.future bestIdp,
       bestSol)
    end

  (* Create a new child *)
  fun newChild (node, i) =
    let
      val spaceRef = ref NONE        
      val descr = case status node of
                    Space.BRANCH descr => descr
                  | _ => raise Child

      val (bestId, bestSol) = if bab then getIdSol node else (0, NONE)
      val status = lazy
        let
          val space = updateRef (spaceRef, getWorkingSpace node)
          val _ = Space.commit (space, i, descr)
          val _ =
            (* If a new best solution is present, bound the node. *)
            case bestSol of  (* May trigger computation of lazy value. *)
              NONE => ()
            | SOME best => bound (space, best)
                
          val status = Space.status space
        in
          (* When a solution is found, BOUND *)
          if status = Space.SOLVED andalso bab
            then setBest (Policy.getSolution space)
          else () ;

          status  
        end
    in
      Node {mom    = node ,
            altNum = i ,
            space  = spaceRef ,
            copy   = ref NONE ,
            status = status,
            bestId = bestId ,
            best   = bestSol ,
            depth  = depth node + 1 }
    end

end
