import structure Name          from "x-alice:/lib/rtt/Name"
import structure UnsafeValue   from "x-alice:/lib/system/UnsafeValue"
import structure Reflect       from "x-alice:/lib/system/Reflect"
import structure Label         from "x-alice:/lib/rtt/Label"
import structure Type          from "x-alice:/lib/rtt/Type"
import structure Inf           from "x-alice:/lib/rtt/Inf"
import structure Path          from "x-alice:/lib/rtt/Path"
import structure Fixity        from "x-alice:/lib/rtt/Fixity"
import structure PervasiveType from "x-alice:/lib/rtt/PervasiveType"
import structure AbsTree       from "AbsTree"
import structure Common        from "Common"
import structure Prefs         from "Prefs"
import structure Cache         from "Cache"

(* Type mappers for basic core types : arrows, tuples, records, ... *)

structure Basic =
struct

  val undefinedKid = Common.undefined "Basic-KID"

  datatype tree = datatype AbsTree.tree
  datatype row = datatype AbsTree.row
             
  (* Mapper for default (unknown) constructors. *)
  fun abstract item = AbsTree.simpleText (item, Prefs.absL, Prefs.defaultStyle)

  (* Mapper for functions, functors, and other non-displayable things. *)
  fun arrow item = AbsTree.simpleText (item, Prefs.funL, Prefs.defaultStyle)
  fun funct item = AbsTree.simpleText (item, Prefs.fctL, Prefs.defaultStyle)
  fun functLambda item = AbsTree.simpleText (item, Prefs.fctLambdaL,
                                             Prefs.defaultStyle)

  (* Mapper for products (records, tuples)
   * tagged is a boolean wich tells if the value is a tagged block. *)
  fun prod (depth, item, row, value, tagged) =
    let
      val row' = Cache.rowToRow row
      val length = case row' of 
                     Tuple typVec       => Vector.length typVec
                   | Record (_, typArr) => Array.length typArr
      val widthLimit = !Prefs.widthLimit
      val (limit, width) =
        if length > widthLimit then (true, widthLimit + 1) else (false, length)
      val kids = Array.array (width, undefinedKid)
        
      val (prefix, suffix) =
        case row' of
          Tuple typVec =>
            (AbsTree.makeTuple (depth - 1, limit, kids,
                                typVec, value, tagged, 0) ;
             (AbsTree.openParenthesisL,    (* "(" *)
              AbsTree.closeParenthesisL))  (* ")" *)

        | Record (labVec, typArr) =>
            (AbsTree.makeRecord (depth - 1, limit, kids,
                                 labVec, typArr, value, tagged, 0) ;
             (AbsTree.openBracketL,        (* "{" *)
              AbsTree.closeBracketL))      (* "}" *)

      val tree = Container {desc = item, limit, kids}
    in
      Concat {prefix, tree, suffix, orient = AbsTree.orientSon}
    end

  (* Type inspection helper. Directly from PPValue. *)
  datatype tag_desc = Nullary | Unary of Type.typ | Nary of Type.row

  fun unarg t = case Type.inspect t of
    Type.Apply(t1,t2) =>
      if not(Type.equal(t1, PervasiveType.typ_strict)) then Unary t else
        (case Type.inspect t2 of
           Type.Prod r => if Type.isEmptyRow r then Nullary else Nary r
         | _ => assert false)
  | _ => Unary t      
      
  (* Mapper for sum types. *)
  fun sum (depth, item, row, value) =
    let
      val (labVec, typArr) = Cache.rowToVector row
      val index = UnsafeValue.tag (value, labVec)
      val label = Label.toString (Vector.sub (labVec, index))
      val typ   = Array.sub (typArr, index)
    in
      case unarg typ of
        Nullary    => AbsTree.simpleText (item, label, Prefs.consStyle)
      | Unary typ' =>
          let
            val arg   = UnsafeValue.projTaggedTuple (value, 1, 0)
            val item' = Common.Value (typ', arg)
            val tree  = AbsTree.mapValue (depth - 1, item', typ', arg)
          in
            AbsTree.applicationWithPar (item, label, tree)
          end
      | Nary row'  =>
          let
            val item' = AbsTree.NestedRow (SOME (Type.prod row'))
            val tree  = prod (depth - 1, item', row', value, true)
          in
            AbsTree.applicationNoPar (item, label, tree)
          end
    end

  (* Futures are put in unary containers. *)
  fun future (item, value) =
      let 
	val item' = AbsTree.NestedRow NONE
	val subtree =
	    if Future.isLazy value
	    then AbsTree.simpleText (item', Prefs.lazL, Prefs.lazyStyle)
	    else AbsTree.simpleText (item', Prefs.futL, Prefs.futureStyle)

	val kids = Array.array (1, subtree)
      in
	Container {desc  = item, limit = false, kids}
      end

  (* Read the exception from a failed future. *)
  fun failedFuture (item, depth, value) =
    let
      (* We read the exception. *)
      val e = Reflect.reflect(Future.await value) handle e => Reflect.reflect e
      val typ' = PervasiveType.typ_exn
      val item' = Common.Value (typ', e)
      val tree = AbsTree.mapValue (depth - 1, item', typ', e)
    in
      AbsTree.failedApplication (item, tree)
    end

  (* Modules *)
  val labelStruct = Simple {desc = AbsTree.Struct,
			    rep = ("struct", Prefs.structStyle) }
  val labelEnd    = Simple {desc = AbsTree.Struct,
			    rep = ("end", Prefs.structStyle) }
  val indentPrefix = [("  ", Prefs.defaultStyle)]


  datatype declaration = datatype AbsTree.declaration
  datatype assoc = datatype Fixity.assoc
  datatype fix   = datatype Fixity.fix
  datatype item  = datatype AbsTree.item

  (* Maps a fixity declaration to a prefix. *)
  val pNonfixL = [("nonfix", Prefs.fixStyle)]
  val pPrefix = ("prefix ", Prefs.fixStyle)
  val pPostfix = ("postfix ", Prefs.fixStyle)
  val pInfix = ("infix ", Prefs.fixStyle)
  val pInfixr = ("infixr ", Prefs.fixStyle)
  val pInfixn = ("infixn ", Prefs.fixStyle)
  fun fixPrefix NONFIX = pNonfixL
    | fixPrefix(PREFIX n) = pPrefix ::[(Int.toString n, Prefs.fixStyle)]
    | fixPrefix(POSTFIX n) = pPostfix ::[(Int.toString n, Prefs.fixStyle)]
    | fixPrefix(INFIX(n, LEFT)) = pInfix ::[(Int.toString n, Prefs.fixStyle)]
    | fixPrefix(INFIX(n, RIGHT)) = pInfixr::[(Int.toString n, Prefs.fixStyle)]
    | fixPrefix(INFIX(n,NEITHER)) = pInfixn::[(Int.toString n, Prefs.fixStyle)]

  val pVal = ("val ", Prefs.valStyle)
  val pValL = [pVal]
  val pFunL = [("fun ", Prefs.valStyle)]
  val pMod = ("structure ", Prefs.structStyle)
  val pModL = [pMod]
  val pFctL = [("functor ", Prefs.structStyle)]

  (* From lib/system/DynMatch *)
  fun trName f (Name.ExId s)	= Name.ExId(f s)
    | trName f  n		= n
  fun trLab f l		= Label.fromName(trName f (Label.toName l))
  val trValLab		= trLab(fn s => s)
  val trTypLab		= trLab(fn s => "$" ^ s)
  val trModLab		= trLab(fn s => s ^ "$")
  val trInfLab		= trLab(fn s => "$" ^ s ^ "$")

  fun getVal (modl, label) = UnsafeValue.projPoly (modl, trValLab label)
  fun getMod (modl, label) = UnsafeValue.projPoly (modl, trModLab label)

  (* Maps a declaration to an abstract tree. *)
  fun mapDecl (modl, depth, decl) =
      case decl of
	FixItem (l, f) =>
	let 
	  val label = " " ^ Label.toString l
	  val tree = AbsTree.simpleText (Decl decl, label, Prefs.labStyle)
	  val prefix = fixPrefix f
	in
	  Concat {prefix, tree, suffix = [], orient = AbsTree.orientSon}
	end
	  
      | ValItem (label, typ, value) =>
	let
          val tree = AbsTree.simpleText (Decl decl, label, Prefs.labStyle)
          val prefix = if Type.isArrow typ then pFunL else pValL
	in
	  Concat {prefix, tree, suffix=[], orient = AbsTree.orientSon}
	end

      | TypItem => AbsTree.simpleText (Decl decl, "type ... not done",
                                       Prefs.labStyle)
      | ModItem (label, msig, mvalue) =>
        let
          val tree = AbsTree.simpleText (Decl decl, label, Prefs.labStyle)
          val prefix = if Inf.isArrow msig then pFctL else pModL
        in
          Concat {prefix, tree, suffix=[], orient = AbsTree.orientSon}
        end

      | SigItem => AbsTree.simpleText (Decl decl, "signature ... not done",
                                       Prefs.labStyle)

  (* Expands a declaration. *)
  fun fromDecl (module, decl) =
      case decl of
	FixItem _ => NONE
      | ValItem (l, t, value) => 
          if Type.isArrow t then NONE else
            let
              val prefix = pVal :: (l, Prefs.labStyle) :: AbsTree.equalL
              val item = Common.Value (t, value)
              val tree = AbsTree.mapValue (!Prefs.incrDepth, item, t, value)
            in
              SOME (Concat {prefix, tree, suffix = [],
                            orient = AbsTree.orientSonStrict})
            end

      | TypItem =>
	SOME (AbsTree.simpleText (Decl TypItem, "type ... still not done", Prefs.labStyle))

      | ModItem (l, msig, mvalue) =>
        if Inf.isArrow msig then NONE else
          let
            val prefix = pMod :: (l, Prefs.labStyle) :: AbsTree.equalL
            val item = Common.Module (msig, mvalue)
            val tree = AbsTree.map (!Prefs.incrDepth, item)
          in
            SOME (Concat {prefix, tree, suffix = [],
                          orient = AbsTree.orientSonStrict})
          end

      | SigItem =>
        SOME (AbsTree.simpleText (Decl SigItem, "signature ... still not done", Prefs.labStyle))

  fun addDecl (_, _, _, index, size, []) = (assert index = size ; [])
    | addDecl (modl, depth, kids, index, size, (el :: elems) as rest) =
      if index >= size then rest (* Finished. *)
      else
	(Array.update (kids, index, mapDecl (modl, depth, el)) ;
	 addDecl (modl, depth, kids, index + 1, size, elems))

  fun makeDecl (module, depth, kids, items, length, limit) =
      let
	val width = Array.length kids
	val size = width - (if limit then 1 else 0)
	val rest = addDecl (module, depth, kids, 0, size, items)
      in
	assert (List.length rest = length - size) ; (* Can be removed. *)
	if limit then 
	  let val limitDesc = AbsTree.ModuleLimit (rest, length - size)
	  in
	    Array.update (kids, size, WidthLimit limitDesc)
	  end
	else ()
      end

  (* Maps Inf items to Abstree declarations. *)
  fun mapItem (modl, Inf.FixItem (l, f)) = SOME (FixItem (l, f))
    | mapItem (modl, Inf.ValItem (lab, typ)) =
      let val label = Label.toString lab
      in
        if String.sub (label, 0) = #"'" then NONE
        else SOME (ValItem (label, typ, getVal (modl, lab)))
      end
    | mapItem (modl, Inf.TypItem _) = SOME TypItem
    | mapItem (modl, Inf.ModItem (path, inf)) =
      let
        val lab   = Path.toLab path
        val label = Label.toString lab
      in
        SOME (ModItem (label, inf, getMod (modl, lab)))
      end
    | mapItem (modl, Inf.InfItem _) = SOME SigItem
    | mapItem (modl, Inf.HiddenItem _) = NONE

  fun filterItems (modl, [], acc, n) = (acc, n)
    | filterItems (modl, x :: xs, acc, n) =
          case mapItem (modl, Inf.inspectItem x) of
	    NONE => filterItems (modl, xs, acc, n)
	  | SOME i => filterItems (modl, xs, i :: acc, n + 1)

  (* Maps a module to an abstree. *)
  fun module (depth, item, sign, module) =
    let
      val (items, length) = filterItems (module, Inf.items sign, [], 0)
      val widthLimit = !Prefs.widthLimit
      val (limit, width) =
        if length > widthLimit then (true, widthLimit + 1) else (false, length)
      val elemKids = Array.array (width, undefinedKid)
      val elems = Container {desc = AbsTree.Struct, limit, kids = elemKids}

      val kids = Array.array (3, undefinedKid)
      val body = Concat {prefix = indentPrefix, tree = elems,
                         suffix = if length = 1 then indentPrefix else [],
                         orient = AbsTree.orientParent}
    in
      Array.update (kids, 0, labelStruct) ;
      Array.update (kids, 1, body) ;
      Array.update (kids, 2, labelEnd) ;
      makeDecl (module, depth - 1, elemKids, items, length, limit) ;
      if length = 0 then AbsTree.simpleText (item, "()", Prefs.structStyle)
      else Container {desc = item, limit = false, kids}
    end


end
