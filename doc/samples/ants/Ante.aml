import signature ANTE from "ANTE-sig"
import structure Instructions from "Instructions"
import functor MkRedBlackMap from "x-alice:/lib/data/MkRedBlackMap"
import structure UnsafeValue from "x-alice:/lib/system/UnsafeValue"

structure Ante : ANTE =
struct
    exception Debug of string

    open Promise

    type marker		= int
    datatype side	= left | right
    datatype dir	= here | ahead | leftahead | rightahead
    datatype condition	= friend | foe | friendwithfood | foewithfood |
			  food | rock | marker of marker | foemarker |
			  home | foehome

    type label		= string
    datatype yesno	= ifyes | ifnot
    datatype otherwise	= otherwise
    datatype instr	= Fail | Concat of instr * instr |
			  Goto of label | Label of label * instr |
			  Mark of marker * instr | Unmark of marker * instr |
			  Turn of side * instr | Move of instr * instr |
			  Pick of instr * instr | Drop of instr |
			  Sense of dir * condition * instr * instr |
			  Flip of int * instr * instr
    type stmt		= int * (instr -> instr)
    type 'a branch	= 'a * stmt


  (* Label generator *)

    val labcount = ref 0

    fun freshlabel () =
	"$" ^ Int.toString (!labcount) before (labcount := !labcount +1)


  (* Primitives *)

    exception Malformed of string

    infixr ++
    fun (0,f1) ++ (n2,f2) = (n2, fn k => Concat(f1 k, f2 k))
      | (1,f1) ++ (n2,f2) = (n2, fn k => f1 (f2 k))
      | (_,f1) ++ (n2,f2) = let val l = freshlabel ()
			    in (n2, fn k => Concat(f1 (Goto l), Label(l, f2 k)))
			    end

    val fail		= (0, fn k => Fail)
    fun goto l		= (0, fn k => Goto l)
    val nop		= (1, id)

    fun label l		= (1, fn k => Label(l, k))
    fun mark m		= (1, fn k => Mark(m, k))
    fun unmark m	= (1, fn k => Unmark(m, k))
    fun turn s		= (1, fn k => Turn(s, k))
    val drop		= (1, fn k => Drop k)

    fun move ifyes (n,f)	= (n+1, fn k => Move(f k, k))
      | move ifnot (n,f)	= (n+1, fn k => Move(k, f k))
    fun pick ifyes (n,f)	= (n+1, fn k => Pick(f k, k))
      | pick ifnot (n,f)	= (n+1, fn k => Pick(k, f k))
    fun sense d c ifyes (n,f)	= (n+1, fn k => Sense(d, c, f k, k))
      | sense d c ifnot (n,f)	= (n+1, fn k => Sense(d, c, k, f k))
    fun flip i ifyes (n,f)	= (n+1, fn k => Flip(i, f k, k))
      | flip i ifnot (n,f)	= (n+1, fn k => Flip(i, k, f k))

    fun join g (n1,f1) (n2,f2)	= (n1+n2, fn k => g (f1 k, f2 k))

    fun move2 ifyes b1 ifnot b2		= join Move b1 b2
      | move2 ifnot b1 ifyes b2		= join Move b2 b1
      | move2 _     _  _     _		= raise Malformed "move"

    fun pick2 ifyes b1 ifnot b2		= join Pick b1 b2
      | pick2 ifnot b1 ifyes b2		= join Pick b2 b1
      | pick2 _     _  _     _		= raise Malformed "pick"

    fun sense2 d c ifyes b1 ifnot b2	= join (fn (i1,i2) => Sense(d,c,i1,i2)) b1 b2
      | sense2 d c ifnot b1 ifyes b2	= join (fn (i1,i2) => Sense(d,c,i1,i2)) b2 b1
      | sense2 _ _ _     _  _     _	= raise Malformed "sense"

    fun flip2 n ifyes b1 ifnot b2	= join (fn (i1,i2) => Flip(n,i1,i2)) b1 b2
      | flip2 n ifnot b1 ifyes b2	= join (fn (i1,i2) => Flip(n,i1,i2)) b2 b1
      | flip2 n _     _  _     _	= raise Malformed "flip"


  (* Combinators *)

    fun on x y = (x,y)

    fun flipn bs		= flipn' (List.length bs) bs
    and flipn' _ []		= nop
      | flipn' _ [b]		= b
      | flipn' _ [b1,b2]	= flip2 2 ifyes b1 ifnot b2
      | flipn' n (b::bs)	= flip2 n ifyes b ifnot (flipn' (n-1) bs)

    fun sensen [] otherwise b	= b
      | sensen (((d,c),b')::bs) otherwise b =
	    sense2 d c ifyes b' ifnot (sensen bs otherwise b)

    fun loop b =
	let
	    val l = freshlabel ()
	in
	    label l ++ b ++ goto l
	end

    fun loopx f =
	let
	    val l1 = freshlabel ()
	    val l2 = freshlabel ()
	in
	    label l1 ++ f (goto l2) ++ goto l1 ++ label l2
	end

    fun times 0 b = nop
      | times n b = b ++ times (n-1) b

    local
    fun timesi' size 1 f = (f (size-1))
      | timesi' size n f = (f (size-n) ++ timesi' size (n-1) f)
    in
    fun timesi n f = timesi' n n f
    end

    fun repeat n b =
	loopx (fn break => flip2 n ifnot b ifyes break)

    fun try f otherwise b =
	let
	    val l1 = freshlabel ()
	    val l2 = freshlabel ()
	in
	    f (goto l1) ++ goto l2 ++ label l1 ++ b ++ label l2
	end

    fun proc l b = label l ++ b


  (* Complex abstractions *)

    fun turnto c ifyes b =
	let
	    val l1 = freshlabel ()
	    val l2 = freshlabel ()
	in
	    sensen [
		on(ahead, c) nop
	    ,
		on(leftahead, c) (turn left)
	    ,
		on(rightahead, c) (turn right)
	    ] otherwise (goto l2) ++
	    label l1 ++ b ++
	    label l2
	end
      | turnto c ifnot b =
	    sensen [
		on(ahead, c) nop
	    ,
		on(leftahead, c) (turn left)
	    ,
		on(rightahead, c) (turn right)
	    ] otherwise b
	

  (* Compilation *)

    structure I = Instructions
    structure E = MkRedBlackMap String

    fun traverse n e a i =
	case i
	 of Fail =>
	    (n, [I.Flip(1,n,n,a)], n+1, e)
	  | Concat(i1,i2) =>
	    let
		val (s1,l1,n1,e1) = traverse n e a i1
		val (s2,l2,n2,e2) = traverse n1 e1 a i2
	    in
		(s1, l1@l2, n2, e2)
	    end
	  | Goto l =>
	    (case E.lookup (e,l)
	     of SOME (p,b) => (future p, [], n, e)
	      | NONE =>
		let
		    val p = promise ()
		in
		    (future p, [], n, E.insert (e,l,(p,false)))
		end
	    )
	  | Label(l,i') =>
	    let
		val p = case E.lookup (e,l)
			 of SOME (p,true) => raise Malformed ("label " ^ l)
			  | SOME (p,false) => p
			  | NONE => promise ()
		val e = E.insert(e,l,(p,true))
		val result as (s',l',n',e') = traverse n e a i'
	    in
		if UnsafeValue.same (s', future p)
		then (fulfill(p, n); (n, I.Flip(1,n,n,a)::l', n'+1, e'))
		else (fulfill(p, s'); result)
	    end
	  | Mark(m,i') =>
	    let
		val (s',l',n',e') = traverse (n+1) e a i'
	    in
		(n, I.Mark(m,s',a)::l', n', e')
	    end
	  | Unmark(m,i') =>
	    let
		val (s',l',n',e') = traverse (n+1) e a i'
	    in
		(n, I.Unmark(m,s',a)::l', n', e')
	    end
	  | Turn(t,i') =>
	    let
		val (s',l',n',e') = traverse (n+1) e a i'
		val t' = case t of left => I.Left | right => I.Right
	    in
		(n, I.Turn(t',s',a)::l', n', e')
	    end
	  | Move(i1,i2) =>
	    let
		val (s1,l1,n1,e1) = traverse (n+1) e a i1
		val (s2,l2,n2,e2) = traverse n1 e1 a i2
	    in
		(n, I.Move(s1,s2,a)::l1@l2, n2, e2)
	    end
	  | Pick(i1,i2) =>
	    let
		val (s1,l1,n1,e1) = traverse (n+1) e a i1
		val (s2,l2,n2,e2) = traverse n1 e1 a i2
	    in
		(n, I.PickUp(s1,s2,a)::l1@l2, n2, e2)
	    end
	  | Drop(i') =>
	    let
		val (s',l',n',e') = traverse (n+1) e a i'
	    in
		(n, I.Drop(s',a)::l', n', e')
	    end
	  | Sense(d,c,i1,i2) =>
	    let
		val (s1,l1,n1,e1) = traverse (n+1) e a i1
		val (s2,l2,n2,e2) = traverse n1 e1 a i2
		val d' = case d of here => I.Here
				 | ahead => I.Ahead
				 | leftahead => I.LeftAhead
				 | rightahead => I.RightAhead
		val c' = case c of friend => I.Friend
				 | foe => I.Foe
				 | friendwithfood => I.FriendWithFood
				 | foewithfood => I.FoeWithFood
				 | food => I.Food
				 | rock => I.Rock
				 | marker m => I.Marker m
				 | foemarker => I.FoeMarker
				 | home => I.Home
				 | foehome => I.FoeHome
	    in
		(n, I.Sense(d',s1,s2,c',a)::l1@l2, n2, e2)
	    end
	  | Flip(j,i1,i2) =>
	    let
		val (s1,l1,n1,e1) = traverse (n+1) e a i1
		val (s2,l2,n2,e2) = traverse n1 e1 a i2
	    in
		(n, I.Flip(j,s1,s2,a)::l1@l2, n2, e2)
	    end

    fun compile (_,f) =
	let
	    val (_,l,n,e) = traverse 0 E.empty () (f Fail)
	in
	    assert n <= 10_000 do
	    assert E.all (Future.isDetermined o future o #1) e do
	    Vector.fromList l
	end
end



(*
    fun continue k (Cont)		= k
      | continue k (Fail)		= Fail
      | continue k (Goto l)		= Goto l
      | continue k (Label(l,k'))	= Label(l, continue k k')
      | continue k (Mark(m,k'))		= Mark(m, continue k k')
      | continue k (Unmark(m,k'))	= Unmark(m, continue k k')
      | continue k (Turn(s,k'))		= Turn(s, continue k k')
      | continue k (Move(k1,k2))	= Move(continue k k1, continue k k2)
      | continue k (Pick(k1,k2))	= Pick(continue k k1, continue k k2)
      | continue k (Drop k')		= Drop(continue k k')
      | continue k (Sense(d,c,k1,k2))	= Sense(d, c, continue k k1,
						      continue k k2)
      | continue k (Flip(n,k1,k2))	= Flip(n,continue k k1, continue k k2)
*)
