(*
 * Authors:
 *   Sebastian Germesin <germi@ps.uni-sb.de>
 *
 * Copyright:
 *   Sebastian Germesin, 2004
 *
 * Last change:
 *   $Date$ by $Author$
 *   $Revision$
 *)

import signature TESTSUITE from "../../src/TESTSUITE-sig"

import structure Gen       from "x-alice:/lib/test/Gen"
import structure Test      from "../../src/Test"

structure TestWord8 :> TESTSUITE = (* the word8 test suite *)
struct

    val randomize = Gen.randomize

    fun wordToIntInf w = valOf (StringCvt.scanString 
				(IntInf.scan StringCvt.HEX) (Word8.toString w))

    (*  p = 2 ^ wordSize *)
    val p = IntInf.pow (IntInf.fromInt 2, Word8.wordSize)

    (* p'  = 2 ^ (wordSize - 1) *)
    val p' = IntInf.pow (IntInf.fromInt 2, Word8.wordSize - 1)

    (* p''  = ~ (2 ^ (wordSize - 1) - 1) *)
    val p'' = ~ (IntInf.pow (IntInf.fromInt 2, Word8.wordSize - 1) - 
		 IntInf.fromInt 1)

    fun testWordSize () =
	Test.test (fn x => x = 8) Word8.wordSize

    fun testToLarge () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testToLargeX () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testFromLarge () =
	let
	    fun t w = w = Word8.fromLarge (Word8.toLarge w)
	in
	    Gen.try 1000 Gen.word8 (Test.test t)
	end

    fun testToInt () = 
	let
	    fun t x = x <= p - (IntInf.fromInt 1) andalso 
		      x >= IntInf.fromInt 0
	in
	    Gen.try 1000 Gen.word8
		    (Test.test (fn w => t (wordToIntInf w)))
	end

    fun testToIntX () = 
	let
	    fun t x = x <= p - (IntInf.fromInt 1) andalso
		      x >=  p''
	in
	    Gen.try 1000 Gen.word8
		    (Test.test (fn w => t (wordToIntInf w)))
	end

    fun testFromInt () =
	let
	    fun t w = w = Word8.fromInt (Word8.toInt w)
	in
	    Gen.try 1000 Gen.word8 (Test.test t)
	end

    fun testAndB () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testOrB () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testXorB () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testNotB () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testLeftShift () =
	let
	    (* i >> n == (i* 2^(n))(mod (2^(wordSize))) *)
	    fun t (i, n) = (IntInf.fromInt i * 
			    IntInf.pow (IntInf.fromInt 2, n)) mod p
	in
	    Gen.try 1000 (Gen.pair (Gen.word8, Gen.word)) 
		    (Test.test (fn (i, n) => wordToIntInf (Word8.<< (i, n)) = 
					     t (Word8.toInt i, 
						Word.toInt n)))
	end

    fun testRightShift () = 
	let
	    (* i >> n == floor(i / (2 ^ n)) *)
	    fun t (i, n) =  IntInf.div (IntInf.fromInt i,
					IntInf.pow (IntInf.fromInt 2, 
						    n))
	in
	    Gen.try 1000 (Gen.pair (Gen.word8, Gen.word)) 
		    (Test.test (fn (i, n) => wordToIntInf (Word8.>> (i, n)) = 
					     t (Word8.toInt i, 
						Word.toInt n)))
	end

    fun testNotRightShift () = 
	Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testArithmetic () =
	let
	    fun tPlus (i, j)  = ((i + j) mod p)
	    fun tMinus (i, j) = ((p + i - j) mod p)
	    fun tTimes (i, j) = ((i * j) mod p)
	    fun tDiv (i, j)   = ((i div j) mod p)
	    fun tMod (i, j)   = ((i - j * (i div j)) mod p)

	    fun t (i, j) =
		let 
		    val (a, b) = (wordToIntInf i, wordToIntInf j)
		in
		    (wordToIntInf (i + j)   = tPlus  (a, b) andalso
		     wordToIntInf (i - j)   = tMinus (a, b) andalso
		     wordToIntInf (i * j)   = tTimes (a, b) andalso
		     wordToIntInf (i div j) = tDiv   (a, b) andalso
		     wordToIntInf (i mod j) = tMod   (a, b)) 
		               handle Div => b = (IntInf.fromInt 0)
		end
	in
	    Gen.try 1000 (Gen.pair (Gen.word8, Gen.word8)) (Test.test t)
	end

    fun testCompare () = 
	let
	    fun t (i, j) = IntInf.compare (wordToIntInf i,
					   wordToIntInf j)
	in
	    Gen.try 1000 (Gen.pair (Gen.word8, Gen.word8))
	      (Test.test (fn (i, j) => Word8.compare (i, j) = t (i, j)))
	end

    fun testCompare' () = 
	let
	    fun t' (i, j) = IntInf.< (wordToIntInf i,
				      wordToIntInf j)

	    fun t'' (i, j) = IntInf.> (wordToIntInf i,
				       wordToIntInf j)

	    fun t''' (i, j) = (wordToIntInf i = wordToIntInf j)

	    fun t (i, j) = i < j = t'   (i, j) andalso
			   i > j = t''  (i, j) andalso
			   i = j = t''' (i, j)
	in
	    Gen.try 1000 (Gen.pair (Gen.word8, Gen.word8))
	      (Test.test t)
	end

    fun testEqual () = 
	let
	    fun t (i, j) = IntInf.equal (wordToIntInf i,
					 wordToIntInf j)
	in
	    Gen.try 1000 (Gen.pair (Gen.word8, Gen.word8))
	      (Test.test (fn (i, j) => Word8.equal (i, j) = t (i, j)))
	end

    fun testScan () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testMin () =
	let
	    fun t (i : Word8.word, j) = if i < j then i else j
	in
	    Gen.try 1000 (Gen.pair (Gen.word8, Gen.word8))
	      (Test.test (fn x => Word8.min x = t x))
	end

    fun testMax () =
	let
	    fun t (i : Word8.word, j) = if i > j then i else j
	in
	    Gen.try 1000 (Gen.pair (Gen.word8, Gen.word8))
	      (Test.test (fn x => Word8.max x = t x))
	end

    fun testFmt () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testToString () = 
	let
	    fun t i = Word8.fmt StringCvt.HEX i
	in 
	    Gen.try 1000 Gen.word8 
	       (Test.test (fn w => Word8.toString w = t w))
	end

    fun testScan () = Test.test (fn _ => raise Test.NotYetImplemented) ()

    fun testFromString () = 
	let
	    fun t s = SOME (StringCvt.scanString (Word8.scan StringCvt.HEX) s)
		          handle Overflow => NONE
	in 
	    Gen.try 1000 Gen.string
	       (Test.test (fn s => 
			      let
				  val r = t s
			      in 
				  if Option.isSome r
				  then Word8.fromString s = (valOf r)
				  else (Word8.fromString s; false)
	                                   handle Overflow => true
			      end))
	end

    val suite = ("Word8", [("wordSize", testWordSize),
			   ("toLarge", testToLarge),
			   ("toLargeX", testToLargeX),
			   ("fromLarge", testFromLarge),
			   ("toInt", testToInt),
			   ("toIntX", testToIntX),
			   ("fromInt", testFromInt),
			   ("andb", testAndB),
			   ("orb", testOrB),
			   ("xorb", testXorB),
			   ("notb", testNotB),
			   ("<<", testLeftShift),
			   (">>", testRightShift),
			   ("~>>", testNotRightShift),
			   ("scan", testScan),
			   ("+", testArithmetic),
			   ("-", testArithmetic),
			   ("*", testArithmetic),
			   ("div", testArithmetic),
			   ("mod", testArithmetic),
			   ("compare", testCompare),
			   ("<", testCompare'),
			   (">", testCompare'),
			   ("=", testCompare'),
			   ("equal", testEqual),
			   ("min", testMin),
			   ("max", testMax),
			   ("fmt", testFmt),
			   ("toString", testToString),
			   ("fromString", testFromString),
			   ("scan", testScan)])
end
