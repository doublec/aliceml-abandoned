(*
 * Authors:
 *   Thorsten Brunklaus <brunklaus@ps.uni-sb.de>
 *
 * Copyright:
 *   Thorsten Brunklaus, 2001
 *
 * Last Change:
 *   $Date$ by $Author$
 *   $Revision$
 *
 *)

import structure OS           from "x-alice:/lib/system/OS"
import structure FD           from "x-alice:/lib/constraints/FD"
import structure FS           from "x-alice:/lib/constraints/FS"
import structure Space        from "x-alice:/lib/constraints/Space"
import structure Packer       from "x-alice:/lib/distribution/Packer"
import structure Remote       from "x-alice:/lib/distribution/Remote"
import structure MkTraceSpace from "MkTraceSpace"
import structure Server       from "Server"
import signature PLAIN_WORKER from "PLAIN_WORKER-sig"
import signature MANAGER      from "MANAGER-sig"
import signature SCRIPT       from "SCRIPT-sig"

functor MkPlainWorker(structure Manager : MANAGER
		      val id : int
		      val script : Packer.package) : PLAIN_WORKER =
    struct
	(* Import Problem *)
	structure MkScript = Packer.Unpack(val package = script signature S = SCRIPT
					       where type sol = Manager.sol)
	structure Script   = MkScript.MkScript(structure FD = FD structure FS = FS)

	(* Build Type Converter *)
	structure TC =
	    struct
		type sol = Manager.sol
		type internal_sol = Script.internal_sol

		val internalize = Script.internalize
		val externalize = Script.externalize
	    end

	(* Build appropriate TraceSpace Module *)
	structure TraceSpace = MkTraceSpace(structure T = TC)
	    
	(* Import Problem Types *)
	type sol          = TC.sol
	type internal_sol = TC.internal_sol
	type trace        = Manager.trace

	(* PlainWorker Datatype Definition *)
	type worker = {root     : TraceSpace.tracespace,
		       id       : int,
		       wopen    : TraceSpace.tracespace list ref,
		       overhead : int ref,
		       nodes    : int ref,
		       task_id  : int ref}

	(* PlainWorker Instance *)
	val self  = {root     = TraceSpace.tracespace(Script.script, NONE),
		     id       = id,
		     wopen    = ref nil,
		     overhead = ref 0,
		     nodes    = ref 0,
		     task_id  = ref 0} : worker

	(* Lock Substitute *)
	val serve = Server.server ()
	    
	(* PlainWorker Methods Implementation *)
	fun logMesg s =
	    let
		val {id=id, ...} = self
	    in
		Manager.log(id, Remote.now (), s)
	    end

	local
	    fun updateExploreState(wopen, s, sr) =
		(case TraceSpace.ask s of
		     Space.FAILED          => wopen := sr
		   | Space.SUCCEEDED       => (Manager.succeeded(TraceSpace.merge s); wopen := sr)
		   | Space.ALTERNATIVES(n) =>
			 let
			     val c = TraceSpace.clone s
			 in
			     (TraceSpace.commit(s, Space.SINGLE 1);
			      TraceSpace.commit(c, Space.RANGE(2, n));
			      wopen := s::c::sr)
			 end)
	in
	    fun exploreMesg () =
		let
		    val {id=id, wopen=wopen, nodes=nodes, ...} = self
		in
		    (case !wopen of
			 nil   => (logMesg "no work left; sending idle";
				   Manager.idle id)
		       | s::sr => (logMesg "exploring";
				   nodes := (!nodes + 1);
				   updateExploreState(wopen, s, sr);
				   serve exploreMesg))
		end
	end

	fun startMesg(ts, id) =
	    let
		val {root=rootspace, overhead=overhead, wopen=wopen,
		     nodes=nodes, task_id=task_id, ...} = self
		val clone = TraceSpace.clone rootspace
	    in
		(TraceSpace.internalize(clone, ts);
		 overhead := length ts;
		 wopen := [clone];
		 nodes := 0;
		 task_id := id;
		 serve exploreMesg)
	    end
	
	local
	    fun canSteal ss =
		(case ss of
		     nil    => false
		   | _::nil => false
		   | _::sr  => true)
		     
	    fun steal (s1::s1r, p) =
		(case s1r of
		     nil  => (Promise.fulfill(p, s1); nil)
		   | _::_ => s1::steal(s1r, p))
	in
	    fun stealMesg v =
		let
		    val {wopen=wopen, overhead=overhead, nodes=nodes, task_id=task_id, ...} = self
		in
		    if canSteal (!wopen) then
			let
			    val s = Promise.promise ()
			in
			    (logMesg "sharing work";
			     wopen := steal(!wopen, s);
			     Promise.fulfill(v, SOME (TraceSpace.externalize (Promise.future s),
						      (!task_id),
						      ((!nodes) + (!overhead)))))
			end
		    else (logMesg "no work to share";
			  Promise.fulfill(v, NONE))
		end
	end

	fun stopMesg () =
	    let
		val {wopen=wopen, ...} = self
	    in
		(logMesg "stopped";
		 wopen := nil)
	    end

	fun shutdownMesg () = OS.Process.exit(OS.Process.success)

	(* PlainWorker Interface Functions *)
	val start    = Remote.proxy (fn v => serve (fn () => startMesg v))
	val explore  = Remote.proxy (fn () => serve exploreMesg)
	val steal    = Remote.proxy (fn v => serve (fn () => stealMesg v))
	val stop     = Remote.proxy (fn () => serve stopMesg)
	val shutdown = Remote.proxy (fn () => serve shutdownMesg)
    end
