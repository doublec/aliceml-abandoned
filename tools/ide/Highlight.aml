(*
 * Authors:
 *   Andreas Rossberg <rossberg@ps.uni-sb.de>
 *
 * Copyright:
 *   Andreas Rossberg, 2005
 *
 * Last Change:
 *   $Date$ by $Author$
 *   $Revision$
 *
 *)

import functor MkRedBlackSet from "x-alice:/lib/data/MkRedBlackSet"

signature HIGHLIGHT =
sig
    datatype style = PLAIN | RESERVED | STRING | COMMENT | ERRONEOUS

    val scan : string list -> (style * int * int * int * int) list
end

structure Highlight : HIGHLIGHT =
struct
    datatype style = PLAIN | RESERVED | STRING | COMMENT | ERRONEOUS

    (* Reserved words *)

    structure SubstringSet = MkRedBlackSet Substring

    val reservedWords = (SubstringSet.fromVector o Vector.map Substring.full)
	#["_file_", "_line_",
	  "abstype", "and", "andalso", "any", "as", "assert",
	  "assert0", "assert1", "assert2", "assert3", "assert4",
	  "assert5", "assert6", "assert7", "assert8", "assert9",
	  "case", "constructor",
	  "datatype", "do",
	  "else", "end", "eqtype", "exception", "exttype",
	  "fct", "finally", "fn", "from", "fun", "functor",
	  "handle",
	  "if", "import", "in", "include", "infix", "infixr",
	  "lazy", "let", "local",
	  "non", "nonfix",
	  "of", "op", "open", "orelse",
	  "pack",
	  "raise", "rec",
	  "sharing", "sig", "signature", "spawn", "struct", "structure",
	  "then", "type",
	  "unpack",
	  "val",
	  "where", "while", "with", "withfun", "withtype", "withval"
	 ]

    fun isReserved ss = SubstringSet.member (reservedWords, ss)

    fun isIdChar c = Char.isAlphaNum c orelse c = #"_"

    fun checkReserved (s, y, x1, x2, rs) =
	if isReserved (Substring.substring (s, x1, x2-x1))
	then (RESERVED, y,x1, y,x2)::rs
	else rs

    (* Scanning *)

    fun scanNLInner (cont, nil, y', x', y, x, rs) =
	    (ERRONEOUS, y',x', y,x)::rs
      | scanNLInner (cont, s::ss, y', x', y, x, rs) =
	    cont (ss, s, y', x', y+1, 0, rs)

    fun scanNL (nil, y, rs)   = rs
      | scanNL (s::ss, y, rs) = scanPlain (ss, s, y+1, 0, rs)

    and scanPlain (ss, s, y, x, rs) =
	if x = String.size s then
	    scanNL (ss, y, rs)
	else let
	    val c = String.sub (s, x)
	in
	    if isIdChar c then
		scanId (ss, s, y, x, x+1, rs)
	    else if c = #"\"" then
		scanString (ss, s, y, x, y, x+1, rs)
	    else if c = #"(" andalso x < String.size s
		 andalso String.sub (s, x+1) = #"*" then
		scanComment 1 (ss, s, y, x, y, x+2, rs)
	    else
		scanPlain (ss, s, y, x+1, rs)
	end
    and scanString (ss, s, y', x', y, x, rs) =
	if x = String.size s then
	    scanNL (ss, y, (ERRONEOUS, y',x', y,x)::rs)
	else let
	    val c = String.sub (s, x)
	in
	    if c = #"\"" then
		scanPlain (ss, s, y, x+1, (STRING, y',x', y,x+1)::rs)
	    else if c = #"\\" then
		if x = String.size s - 1
		orelse Char.isSpace (String.sub (s, x+1))
		then scanStringGap (ss, s, y', x', y, x+1, rs)
		else scanString (ss, s, y', x', y, x+2, rs)
	    else
		scanString (ss, s, y', x', y, x+1, rs)
	end
    and scanStringGap (ss, s, y', x', y, x, rs) =
	if x = String.size s then
	    scanNLInner (scanStringGap, ss, y', x', y, x, rs)
	else let
	    val c = String.sub (s, x)
	in
	    if c = #"\\" then
		scanString (ss, s, y', x', y, x+1, rs)
	    else if Char.isSpace c then
		scanStringGap (ss, s, y', x', y, x+1, rs)
	    else
		scanPlain (ss, s, y, x+1, (ERRONEOUS, y',x', y,x+1)::rs)
	end
    and scanId (ss, s, y, x', x, rs) =
	if x = String.size s then
	    scanNL (ss, y, checkReserved (s, y, x', x, rs))
	else if isIdChar (String.sub (s, x)) then
	    scanId (ss, s, y, x', x+1, rs)
	else
	    scanPlain (ss, s, y, x, checkReserved (s, y, x', x, rs))
    and scanComment nesting (ss, s, y', x', y, x, rs) =
	if nesting = 0 then
	    scanPlain (ss, s, y, x, (COMMENT, y',x', y,x)::rs)
	(* last character cannot end or start a comment *)
	else if x = String.size s - 1 then
	    scanNLInner (scanComment nesting, ss, y', x', y, x, rs)
	else let
	    val c = String.sub (s, x)
	in
	    if c = #"*" andalso String.sub (s, x+1) = #")" then
		scanComment (nesting-1) (ss, s, y', x', y, x+2, rs)
	    else if c = #"(" andalso String.sub (s, x+1) = #"*" then
		scanComment (nesting+1) (ss, s, y', x', y, x+2, rs)
	    else
		scanComment nesting (ss, s, y', x', y, x+1, rs)
	end

    (* Export *)

    fun scan  nil    = nil
      | scan (s::ss) = List.rev (scanPlain (ss, s, 1, 0, nil))
end
