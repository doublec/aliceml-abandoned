(*
 * Authors:
 *   Guido Tack <tack@ps.uni-sb.de>
 *
 * Copyright:
 *   Guido Tack, 2005
 *
 * Last Change:
 *   $Date$ by $Author$
 *   $Revision$
 *
 *)

import structure MkHashImpMap from "x-alice:/lib/data/MkHashImpMap"
import structure Gtk from "x-alice:/lib/gtk/Gtk"
import structure Core from "x-alice:/lib/gtk/Core"
import structure FileDialog from "FileDialog"
import structure Config from "Config"

structure Buffer =
   struct
      open Config

      structure BufferMap = MkHashImpMap(Int)

      datatype undo = INSERT of (int * int)
                    | DELETE of (int * string)
                    | WAIT_INSERT of (int * int)
                    | WAIT_DELETE of (int * string)

      type buffer = {id: int,
                     file: string option ref,
                     fileLabel: Gtk.object,
                     view: Gtk.object,
                     buf: Gtk.object,
                     tags: tags,
                     undo: undo list ref,
                     lastSearchPos: int ref}

      val bufferMap : buffer BufferMap.map = BufferMap.map()

      fun shortName s =
          let
              fun lastSlash i j = if j=size s then i
                                  else if String.sub(s,j)= #"/" then lastSlash j (j+1)
                                  else lastSlash i (j+1)
          in
              String.extract(s,1 + lastSlash ~1 0, NONE)
          end

      (* Cursor *)
      fun getCursorIter buf =
          let
              val cursor = Gtk.textBufferGetInsert buf
              val iter = Gtk.textIterNew()
          in
              Gtk.textBufferGetIterAtMark(buf, iter, cursor);
              iter
          end

      fun selectRange(buf, startiter, enditer) =
	  (Gtk.textBufferMoveMarkByName(buf, "insert", startiter);
	   Gtk.textBufferMoveMarkByName(buf, "selection_bound", enditer))

      fun modifiedChanged id _ =
          let
              val {fileLabel,file,buf,...} = BufferMap.lookupExistent(bufferMap, id)
              val fileName = shortName (Option.getOpt(!file, "New file "^Int.toString id))
          in
              if Gtk.textBufferGetModified buf then
                  Gtk.labelSetText(fileLabel, fileName^"*")
              else
                  Gtk.labelSetText(fileLabel, fileName)
          end

      fun deleteText id (buf,[Gtk.INT off, Gtk.INT _, Gtk.STRING txt]) =
          let
              val {undo,...} = BufferMap.lookupExistent(bufferMap, id)
          in
              case (!undo) of
                  ((WAIT_INSERT _ | WAIT_DELETE _)::ur) =>
                  undo := ur
                | _ =>
                  undo := DELETE(off, txt) :: (!undo)
          end
        | deleteText _ _ = print "Gtk is crazy: delete"

      fun insertText id (buf,[Gtk.INT off, _, Gtk.INT length]) =
          let
              val {undo,...} = BufferMap.lookupExistent(bufferMap, id)
          in
              case (!undo) of
                  ((WAIT_INSERT _ | WAIT_DELETE _)::ur) =>
                  undo := ur
                | _ =>
                  undo := INSERT(off, off+length) :: (!undo)
          end
        | insertText _ _ = print "Gtk is crazy: insert"

      fun getSelection id =
          let
              val {buf,...} = BufferMap.lookupExistent(bufferMap, id)
              val startiter = Gtk.textIterNew()
              val enditer = Gtk.textIterNew()
          in
              Gtk.textBufferGetSelectionBounds(buf, startiter, enditer);
              if Gtk.textIterEqual(startiter, enditer) then NONE
              else SOME (Gtk.textBufferGetText(buf,
                                               startiter,enditer,
                                               false))
          end

      fun undo id =
          let
              val {undo,buf,...} = BufferMap.lookupExistent(bufferMap, id)
              fun checkUnmodified nil =
                  Gtk.textBufferSetModified(buf, false)
                | checkUnmodified ur = ()
          in
              case (!undo) of nil => ()
                            | (DELETE(off,txt)::ur) =>
                              let
                                  val iter = Gtk.textIterNew()
                              in
                                  Gtk.textBufferGetIterAtOffset(buf,
                                                                iter,
                                                                off);
                                  undo := WAIT_DELETE(off,txt) :: ur;
                                  Gtk.textBufferInsert(buf, iter, txt,
                                                       String.size txt);
                                  checkUnmodified ur
                              end
                            | (INSERT(off1,off2)::ur) =>
                              let
                                  val startiter = Gtk.textIterNew()
                                  val enditer = Gtk.textIterNew()
                              in
                                  Gtk.textBufferGetIterAtOffset(buf,
                                                                startiter,
                                                                off1);
                                  Gtk.textBufferGetIterAtOffset(buf,
                                                                enditer,
                                                                off2);
                                  undo := WAIT_INSERT(off1,off2) :: ur;
                                  Gtk.textBufferDelete(buf, startiter,
                                                       enditer);
                                  checkUnmodified ur
                              end
                            | _ => ()
          end

      fun getFileName id =
          !(#file (BufferMap.lookupExistent(bufferMap, id)))

      fun getBuffer id =
          #buf (BufferMap.lookupExistent(bufferMap, id))

      fun getBufferByName f =
	  case BufferMap.find
	      (fn {file,...} => !file=SOME f) bufferMap of
	      NONE => NONE
	    | SOME {buf,...} => SOME buf

      fun getView id =
          #view (BufferMap.lookupExistent(bufferMap, id))

      fun buffer {notebook,id,file,loadFile} =
          let
              val fileName = Option.getOpt(file, "New file "^Int.toString id)
              val scrolledwindow2_xa = Gtk.adjustmentNew(0.0,0.0,0.0,1.0,0.0,0.0)
              val scrolledwindow2_ya = Gtk.adjustmentNew(0.0,0.0,0.0,1.0,0.0,0.0)
              val scrolledwindow2 = Gtk.scrolledWindowNew(scrolledwindow2_xa,scrolledwindow2_ya)
              val _ = Gtk.scrolledWindowSetShadowType(scrolledwindow2,Gtk.SHADOW_NONE)
              val _ = Gtk.widgetShow scrolledwindow2
                      
              val newSource = Gtk.textViewNew()
              val _ = Gtk.widgetShow newSource
              val _ = Gtk.textViewSetEditable(newSource,true)
              val _ = Gtk.textViewSetJustification(newSource,Gtk.JUSTIFY_LEFT)
              val _ = Gtk.textViewSetWrapMode(newSource,Gtk.WRAP_NONE)
              val _ = Gtk.textViewSetCursorVisible(newSource,true)
              val _ = Gtk.textViewSetPixelsAboveLines(newSource,0)
              val _ = Gtk.textViewSetPixelsBelowLines(newSource,0)
              val _ = Gtk.textViewSetPixelsInsideWrap(newSource,0)
              val _ = Gtk.textViewSetLeftMargin(newSource,0)
              val _ = Gtk.textViewSetRightMargin(newSource,0)
              val _ = Gtk.textViewSetIndent(newSource,0)
              val buf = Gtk.textViewGetBuffer newSource
              val _ = Gtk.containerAdd(scrolledwindow2,newSource)
              val label1 = Gtk.labelNew (shortName fileName)
              val _ = Gtk.widgetShow label1
	      val _ = Gtk.notebookAppendPage(notebook, scrolledwindow2, label1)
              val page = Gtk.notebookPageNum(notebook, scrolledwindow2)
              val _ = Gtk.notebookSetCurrentPage(notebook, page)
              val _ = Gtk.widgetModifyFont(newSource, stdFont)

              val ret = {id=id,file=ref file,
                         fileLabel=label1,
                         view=newSource,buf=buf,
                         tags=makeTags buf,
                         undo=ref nil,
                         lastSearchPos=ref 0}
          in
              if loadFile then
              case file of NONE => ()
                         | SOME f =>
                           let
                               val fileD = TextIO.openIn f
                               val text = TextIO.inputAll fileD
                               val text = Core.latin1ToUtf8 text
                               val iter = Gtk.textIterNew()
                           in
                               Gtk.textBufferSetText(buf, text, ~1);
                               Gtk.textBufferSetModified(buf, false);
                               Gtk.textBufferGetStartIter(buf, iter);
                               Gtk.textBufferPlaceCursor(buf, iter)
                           end handle IO.Io _ => FileDialog.message "IO error. Could not open file."
              else ();
              Gtk.signalConnect(buf, "modified-changed", modifiedChanged id);
              Gtk.textBufferConnectSpecialSignals buf;
              Gtk.signalConnect(buf, "delete-text", deleteText id);
              Gtk.signalConnect(buf, "insert-text", insertText id);
              BufferMap.insertDisjoint(bufferMap, id, ret);
              ret
          end

      fun close id =
          let
              val {file,buf,...} = BufferMap.lookupExistent(bufferMap, id)
              val modified = Gtk.textBufferGetModified buf
          in
              if modified then
                  false
              else
                  (BufferMap.removeExistent(bufferMap, id);
                   true)
          end

      fun save id =
          let
              val {id,file,buf,...} = BufferMap.lookupExistent(bufferMap, id)
              val modified = Gtk.textBufferGetModified buf
          in
              case !file of NONE => false
                          | SOME f =>
                            (if modified then
                                 let
                                     val file = TextIO.openOut f
                                     val startiter = Gtk.textIterNew()
                                     val _ = Gtk.textBufferGetStartIter(buf,startiter)
                                     val enditer = Gtk.textIterNew()
                                     val _ = Gtk.textBufferGetEndIter(buf,enditer)
                                     val str = Gtk.textBufferGetText(buf, startiter, enditer, false)
                                     val str = Core.utf8ToLatin1 str
                                 in 
                                     TextIO.output(file, str);
                                     TextIO.closeOut file;
                                     Gtk.textBufferSetModified(buf, false)
                                 end handle IO.Io _ => FileDialog.message "IO Error. Could not save file."
                             else ();
                             true)
          end

      fun saveAs (id, fileName) =
          let
              val {id,file,fileLabel,buf,...} = BufferMap.lookupExistent(bufferMap, id)
          in
              file := SOME fileName;
              Gtk.labelSetText(fileLabel, shortName fileName);
              Gtk.textBufferSetModified(buf, true);
              save id;
              ()
          end

      fun match iter back txt i caseSensitive =
          if (if caseSensitive then
                  Gtk.textIterGetChar iter = ord (String.sub(txt,i))
              else
                  let
                      val c = Gtk.textIterGetChar iter
                      val c = if c<=Char.maxOrd then
                                  ord (Char.toLower (chr c))
                              else c
                  in
                      c = ord (Char.toLower (String.sub(txt,i)))
                  end)
          then if i=String.size txt - 1 then 
                   (Gtk.textIterSetOffset(iter,back);
                    true)
               else if Gtk.textIterForwardChar iter 
               then match iter back txt (i+1) caseSensitive
               else (Gtk.textIterSetOffset(iter,back);
                     false)
          else (Gtk.textIterSetOffset(iter,back);
                false)

      fun find' iter off txt caseSensitive =
          (match iter off txt 0 caseSensitive orelse
           (Gtk.textIterForwardChar iter andalso
            find' iter (off+1) txt caseSensitive))

      fun find id txt caseSensitive =
          let
              val {view,buf,lastSearchPos,...} = BufferMap.lookupExistent(bufferMap, id)
              val iter = getCursorIter buf
              val off = Gtk.textIterGetOffset iter
              val off = if (off = (!lastSearchPos)) andalso Gtk.textIterForwardChar iter
                        then off + 1
                        else off
          in
              if find' iter off txt caseSensitive then
                  let
                      val iter' = Gtk.textIterCopy iter
                  in
                      Gtk.textIterForwardChars(iter',size txt);
                      lastSearchPos := Gtk.textIterGetOffset iter;
                      selectRange(buf,iter,iter');
                      Gtk.textViewScrollToMark(view,
                                               Gtk.textBufferGetInsert buf,
                                               0.0,false,0.0,0.0)
                  end
              else ()
          end
   end
